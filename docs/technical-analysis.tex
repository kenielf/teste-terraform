\documentclass{article}

\usepackage{fancyvrb}
\VerbatimFootnotes
\usepackage{minted}
\usepackage{indentfirst}
\usepackage{hyperref}
\hypersetup{
    colorlinks=true,
    linkcolor=black,
    filecolor=magenta,
    urlcolor=cyan,
    pdftitle={NeoTasker - Documentação},
    pdfpagemode=FullScreen,
}
\urlstyle{same}
\usepackage{titlesec}
\setcounter{secnumdepth}{4} % Use paragraphs as another added depth
\titleformat{\paragraph}
{\normalfont\normalsize\bfseries}{\theparagraph}{1em}{}
\titlespacing*{\paragraph}
{0pt}{3.25ex plus 1ex minus .2ex}{1.5ex plus .2ex}



\begin{document}
\tableofcontents
\pagebreak

\section{Configuração do Terraform para AWS}
Terraform é uma ferramenta de \textbf{infraestrutura como código \textit{IaC}} para orquestrar diferentes componentes de infraestrutura, criando e destruindo recursos, e efetuando modificações quando necessário. Sua configuração é realizada a partir de arquivos com a extensão \Verb{tf}.

Para o escopo definido no arquivo \Verb{main.tf} contido neste repositório, serão criados recursos para a \textbf{Amazon Web Services \textit{(AWS)}} para subir em nuvem uma infraestrutura com recursos de rede e segurança complementando o recurso de \textit{compute}.

\subsection{Provedor}
Localizado ao início do arquivo, a primeira seção define qual é o provedor -- um plugin utilizado para gerenciar os recursos que são declarados na configuração.
\begin{minted}[autogobble]{terraform}
    provider "aws" {
      region = "us-east-1"
    }
\end{minted}

O parâmetro \Verb{region} define em qual região serão criados os recursos, neste caso \Verb{us-east-1} se refere a região de \textbf{Virgínia do Norte, Estados Unidos}.

\subsection{Variáveis}
Logo abaixo, são definidos variáveis que serão reutilizadas em diferentes trechos da configuração.
\begin{minted}[autogobble]{terraform}
    variable "projeto" {
      description = "Nome do projeto"
      type        = string
      default     = "VExpenses"
    }

    variable "candidato" {
      description = "Nome do candidato"
      type        = string
      default     = "ChrystianFranklin"
    }
\end{minted}

Ambas essas variáveis contém campos de descrição, tipo e valor padrão -- que podem ser sobrescritos por variáveis de ambiente, esses valores podem ser sobrescritos em diferentes módulos, ou por argumentos ao executável\footnote{
    \Verb{terraform apply -var="candidato=OutroCandidato"}
}, ou por variáveis de ambiente\footnote{
    \Verb{TF_VAR_candidato="OutroCandidato" terraform apply}
}, ou até mesmo por um \href{https://developer.hashicorp.com/terraform/language/values/variables\#variable-definitions-tfvars-files}{arquivo dedicado de variáveis}.

Apesar de não estar incluso nesse trecho, as variáveis podem conter configuração de validação a partir de condições, podendo verificar diferentes aspectos, como por exemplo o tamanho mínimo e máximo de uma variável e prefixação, e quando não são atendidas, uma mensagem de erro pode ser exibida a partir do parâmetro de \Verb{error_message}.

\subsection{Recursos}
\subsubsection{Par de Chaves TLS}
Nessa seção são criadas dois recursos principais, uma chave privada de TLS e um par de chaves da AWS (a partir da chave TLS), que serão usadas para acessar a instância EC2 que será criada.
\begin{minted}[autogobble]{terraform}
    resource "tls_private_key" "ec2_key" {
      algorithm = "RSA"
      rsa_bits  = 2048
    }
\end{minted}

O algorítmo utilizado para a criação da chave é o RSA com tamanho de 2048 bits, considerado seguro\footnote{
    Uma implementação segura de RSA requer \textit{blinding} para evitar \href{https://www.cs.sjsu.edu/faculty/stamp/students/article.html}{ataques de timing}.
} e utilizado por várias plataformas como tipo de chave principal.

\begin{minted}[autogobble]{terraform}
    resource "aws_key_pair" "ec2_key_pair" {
      key_name   = "${var.projeto}-${var.candidato}-key"
      public_key = tls_private_key.ec2_key.public_key_openssh
    }
\end{minted}

Esse trecho cria o par de chave na AWS utilizando as variáveis de \textbf{projeto} e \textbf{candidato}, a partir da chave pública gerada no trecho anterior, com o formato do OpenSSH.

\subsubsection{Virtual Private Cloud (VPC)}
Essa seção define uma camada essencial para controlar configurações de rede para os recursos, e será a base para definir a subnet, gateway, tabela de rotas, e grupos de segurança.

\begin{minted}[autogobble]{terraform}
    resource "aws_vpc" "main_vpc" {
      cidr_block           = "10.0.0.0/16"
      enable_dns_support   = true
      enable_dns_hostnames = true

      tags = {
        Name = "${var.projeto}-${var.candidato}-vpc"
      }
    }
\end{minted}

A definição do intervalo de endereços é definido usando o padrão CIDR com máscara de 16 bits, permitindo que endereços sejam definidos a partir de 10.0.0.0 até 10.255.255.255 \textit{(RFC 1918)}.

A configuração da VPC é realizada de uma maneira que a resolução de \textit{domain name system (DNS)} seja permitida, facilitando a comunicação de recursos dentro da VPC com o uso de \textit{hostnames} -- nesse caso os nomes das instâncias que serão inicializadas.

\subsubsection{Subnet}
Uma sub-rede é uma divisão lógica de uma rede, dentro do contexto da AWS é utilizada para dividir uma VPC em seções menores e para agrupar recursos.

\begin{minted}[autogobble]{terraform}
    resource "aws_subnet" "main_subnet" {
      vpc_id            = aws_vpc.main_vpc.id
      cidr_block        = "10.0.1.0/24"
      availability_zone = "us-east-1a"

      tags = {
        Name = "${var.projeto}-${var.candidato}-subnet"
      }
    }
\end{minted}

A criação do recurso de sub-rede depende do identificador da VPC anterior, que é obtido a partir do \Verb{aws_vpc.main_vpc.id}.

O seu intervalo é definido com uma máscara de 24 bits, e faz com que os endereços comecem em 10.0.1.0 até 10.0.1.255, permitindo que 254 endereços sejam utilizados para os hosts \textit{excluindo os endereços de rede e de transmissão}.

Sua zona de disponibilidade é definida a partir da região do provedor, nesse caso \Verb{us-east-1a}, e define comportamentos como disponibilidade e resiliência à falhas.

\subsubsection{Gateway}
Um gateway é um recurso gratuito da AWS\footnote{
    Apesar de que instâncias que se comunicam com a internet \href{https://aws.amazon.com/ec2/pricing/on-demand/}{incorrem com custos de transferências}
} que permite a comunição entre as instâncias criadas no VPC e a internet, dado a existência de um endereço de IP público das instâncias, definindo um target na tabela de rotas da VPC.

\begin{minted}[autogobble]{terraform}
    resource "aws_internet_gateway" "main_igw" {
      vpc_id = aws_vpc.main_vpc.id

      tags = {
        Name = "${var.projeto}-${var.candidato}-igw"
      }
    }
\end{minted}

Da mesma maneira como feito na sub-rede, a criação do gateway também depende do identificador da VPC.

\subsubsection{Route Table \& Associations}
A tabela de rotas define as regras de rotas de uma determinada VPC e é usada para determinar o fluxo dos dados em tráfego.

\begin{minted}[autogobble]{terraform}
    resource "aws_route_table" "main_route_table" {
      vpc_id = aws_vpc.main_vpc.id

      route {
        cidr_block = "0.0.0.0/0"
        gateway_id = aws_internet_gateway.main_igw.id
      }

      tags = {
        Name = "${var.projeto}-${var.candidato}-route_table"
      }
    }
\end{minted}

Novamente, por ser um recurso englobado por VPC, a configuração requer o definição desse identificador.

O intervalo de endereços contido indica que todo o tráfego outbound\footnote{
    Tráfego inbound é definido por grupos de segurança e ACLs.
} \textit{(representado pelo bloco CIDR de 0.0.0.0/0)} será redirecionado pelo gateway definido anteriormente. 

\begin{minted}[autogobble]{terraform}
    resource "aws_route_table_association" "main_association" {
      subnet_id      = aws_subnet.main_subnet.id
      route_table_id = aws_route_table.main_route_table.id

      tags = {
        Name = "${var.projeto}-${var.candidato}-route_table_association"
      }
    }
\end{minted}

Define a associação entre a sub-rede e a tabela de rotas, utilizando como configuração os identificadores de ambos. Permitindo com que a sub-rede use as regras de roteamento da tabela. Sendo assim, mais um aspecto do controle de tráfego.

Como o gateway utilizado é um \textit{internet gateway}, que expõe o tráfego à internet, a sub-rede principal na qual é ligada pode se comunicar com a internet, requerendo assim a configuração de um grupo de segurança para limitar o acesso aos recursos.

\subsubsection{Security Group (SSH)}
O grupo de segurança age como uma firewall virtual para efetuar o controle de tráfego inbound e outbound dos recursos a partir de agrupamentos. 
\begin{minted}[autogobble]{terraform}
    resource "aws_security_group" "main_sg" {
      name        = "${var.projeto}-${var.candidato}-sg"
      description = "Permitir SSH de qualquer lugar e todo o tráfego de saída"
      vpc_id      = aws_vpc.main_vpc.id

      # Regras de entrada
      ingress {
        description      = "Allow SSH from anywhere"
        from_port        = 22
        to_port          = 22
        protocol         = "tcp"
        cidr_blocks      = ["0.0.0.0/0"]
        ipv6_cidr_blocks = ["::/0"]
      }

      # Regras de saída
      egress {
        description      = "Allow all outbound traffic"
        from_port        = 0
        to_port          = 0
        protocol         = "-1"
        cidr_blocks      = ["0.0.0.0/0"]
        ipv6_cidr_blocks = ["::/0"]
      }

      tags = {
        Name = "${var.projeto}-${var.candidato}-sg"
      }
}
\end{minted}

Sua configuração também requer a definição do identificador da VPC para associar as regras do grupo à VPC.

\paragraph{Regras de Entrada}
A configuração de regras de entradas é realizada pelo bloco de \Verb{ingress}, configurando o acesso por SSH a partir da porta 22, permitindo o acesso a partir de qualquer endereço de IPv4 e IPv6 \textit{(considerado arriscado)}.

\paragraph{Regras de Saída}
A configuração das regras de saída é realizada pelo bloco de \Verb{egress}, configurando que para quaisquer tráfego outbound \textit{(todas as portas)}, e todos os protocolos \textit{("-1")}, para quaisquer destino IPv4 e IPv6.

\subsubsection{Amazon Machine Image (AMI)}
AMI é um serviço disponibilizado pela AWS para a construção de imagens EC2, contendo o software necessário para configurar e inicializar uma instância, contendo também o mapeamento de \textit{block devices} ligados à instância.

\begin{minted}[autogobble]{terraform}
    data "aws_ami" "debian12" {
      most_recent = true

      filter {
        name   = "name"
        values = ["debian-12-amd64-*"]
      }

      filter {
        name   = "virtualization-type"
        values = ["hvm"]
      }

      owners = ["679593333241"]
    }
\end{minted}

Utilizando o \Verb{most_recent = true} para retornar a imagem mais recente encontrada, a partir do filtro de nome \Verb{"debian-12-amd64-*"} para buscar uma imagem de Debian 12 x86\_64, com o filtro de tipo de virtualização indicando que a imagem deve ser a virtualização de \textit{Hardware Virtual Machine (HVM)}

O parâmetro de \Verb{owners} restringe a pesquisa para buscar AMIs contidos nas contas correspondentes aos IDs passados.

Essa configuração visa garantir que as instâncias criadas a partir desse AMI estejam atualizadas, já que não restringe por ID de AMI e sim por uma consulta.

\subsubsection{Elastic Compute Cloud (EC2)}
Esse serviço da AWS é responsável pelo gerenciamento e disponibilização de capacidade computacional escalável por demanda, e é o componente principal para subir uma aplicação em núvem, já que o software executado será contido em uma instância EC2\footnote{
    A AWS disponibiliza outros serviços que permitem a execução de funções e softwares conteinerizados, porém foge do escopo desse repositório.
}.
\begin{minted}[autogobble]{terraform}
    resource "aws_instance" "debian_ec2" {
      ami             = data.aws_ami.debian12.id
      instance_type   = "t2.micro"
      subnet_id       = aws_subnet.main_subnet.id
      key_name        = aws_key_pair.ec2_key_pair.key_name
      security_groups = [aws_security_group.main_sg.name]

      associate_public_ip_address = true

      root_block_device {
        volume_size           = 20
        volume_type           = "gp2"
        delete_on_termination = true
      }

      user_data = <<-EOF
                  #!/bin/bash
                  apt-get update -y
                  apt-get upgrade -y
                  EOF

      tags = {
    Name = "${var.projeto}-${var.candidato}-ec2"
  }
}
\end{minted}

Utilizando o AMI encontrado anteriormente, é definido o tipo de instância como \Verb{"t2.micro"} -- disponível no Free Tier da AWS -- e configura os parâmetros definidos anteriormente à instância: a sub-rede, o par de chaves e o grupo de segurança.

Definindo também que a instância deve possuir um endereço público, acessível pela internet, e criando um \textit{block device} de 20 gibibytes, com tipo de \textit{"general purpose ssd" (GP2)} e configurando para ser removidos quando a instância é terminada.

O trecho de \Verb{"user_data"} é um shell script executado na inicialização da instância, e define as etapas de configurações iniciais, nesse caso atualizando o sistema.

\subsection{Outputs}
A seção de \textit{outputs} define o conteúdo que deve ser disponibilizado após a criação da infraestrutura.

\subsubsection{Chave de Acesso Privada}
\begin{minted}[autogobble]{terraform}
    output "private_key" {
      description = "Chave privada para acessar a instância EC2"
      value       = tls_private_key.ec2_key.private_key_pem
      sensitive   = true
    }
\end{minted}
Exibindo a chave privada em formato PEM, com o parâmetro \Verb{sensitive = "true"} garantindo que o recurso não seja exibido ou armazenado em logs ou output do console.

\subsubsection{Endereço IP Público}
\begin{minted}[autogobble]{terraform}
    output "ec2_public_ip" {
      description = "Endereço IP público da instância EC2"
      value       = aws_instance.debian_ec2.public_ip
    }
\end{minted}
Exibe o endereço de IP público da instância criada anteriormente, facilitando o acesso ao recurso.

\section{Considerações Adicionais}
\subsection{Nomenclatura de Recursos}
Todos os recursos criados tem característicamente nomes identificáveis, facilitando a organização e o acesso dos parâmetros internos de cada um.

\subsection{Uso de Tags}
A definição das tags nos recursos criados auxilia na organização e identificação dos mesmos, permitindo que esses possam ser buscados no console da AWS, atentando-se ao controle de gastos por esses recursos.

Além da documentação e monitoramento, o uso de tags permite que certas automações sejam implementadas com mais facilidade, como verificação de conformidade à políticas e a integração de scripts de automação.
\end{document}
